// This version of higher lower enforces at most n guesses by c, and thus needs an additional register to track the guess count 
Initial state: (0)
Initial register assignments: rn=0, rt=0, rx=0, rc=0
// A communicates the secret number to B 
(0) a->b:n{0<=n/\rn'=n/\rt'=rt/\rx'=rx/\rc'=rc} (1)
// A communicates the maximum guess count to B 
(1) a->b:t{t>0/\rn'=rn/\rt'=t/\rx=rx'/\rc'=t} (2)
// C sends B a guess, decrementing the guess count 
(2) c->b:x{rx'=x/\rc>0/\rn'=rn/\rt'=rt/\rc'=rc-1} (3)
// Either B sends higher/lower and continues the game
(3) b->c:higher{higher=3/\rn>rx/\rc>0/\rc'=rc/\rn'=rn/\rt'=rt/\rx'=rx} (2)
(3) b->c:lower{lower=2/\rn<rx/\rc>0/\rc'=rc/\rn'=rn/\rt'=rt/\rx'=rx} (2)
// Or B informs C then A that C has won and A has lost, and ends the game 
(3) b->c:won{won=1/\rx=rn/\rn'=rn/\rt'=rt/\rx'=rx/\rc'=rc} (5)
(5) b->a:lost{lost=0/\rn'=rn/\rt'=rt/\rx'=rx/\rc'=rc} (7)
// Or B informs C that C has lost because there are no more guesses left  
(3) b->c:lost{lost=0/\rn!=rx/\rc=0/\rn'=rn/\rt'=rt/\rx'=rx/\rc'=rc} (6)
(6) b->a:won{won=1/\rn'=rn/\rt'=rt/\rx'=rx/\rc'=rc} (7)
Final states: (7)
